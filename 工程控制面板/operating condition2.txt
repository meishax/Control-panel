FINISH 
/CLEAR 
/PREP7 $/PLOPTS,DATE,0	! 进入前处理模块
ET,1,BEAM189 $ET,2,LINK180
*AFUN,DEG	! 定义单元类型
SD=38 
XD=3 
DEG_BACK=38
R_FRONT=11.25 
D_D_HIGH=0.25 
Theta=ASIN(9/R_FRONT)+0.5
*AFUN,DEG $D_FRONT= Theta / SD $L_BACK=1/COS(38) $D_BACK=(L_BACK-0.05)/XD
HZZH2_FRONT_PRES=400 $HZZH2_BACK_PRES=3254 
HZZH2_RIDGE_FK=1120 $HZZH2_LONG_PRES=180
HZZH3_FRONT_PRES=2 $HZZH3_BACK_PRES=3097 
HZZH3_RIDGE_FK=1749 $HZZH3_LONG_PRES=180
HZZH3_FRONT_WIND=82 $HZZH3_BACK_WIND=-152 
HZZH4_FRONT_PRES=72 $HZZH4_BACK_PRES=3097
HZZH4_RIDGE_FK=1120 $HZZH4_LONG_PRES=180 
HZZH4_FRONT_WIND=82 $HZZH4_BACK_WIND=-152
! 荷载组合参数
MP,EX,1,2.0E11 $MP,PRXY,1,0.3 $MP,DENS,1,7850	! 定义材料参数
SECTYPE,1,BEAM,CTUBE $SECDATA,14.75E-3,16.75E-3
SECTYPE,2,BEAM,CTUBE $SECDATA,11.4E-3,13.4E-3
SECTYPE,3,LINK $SECDATA,0.785E-4	! 定义截面参数
! =============================建立模型=============================
K,, 
LOCAL,11,1,9,-R_FRONT*cos(Theta),,90 
*DO,I,Theta,0,-D_FRONT 
K,,R_FRONT,I 
*ENDDO 
CSYS,0
! 前屋面上弦关键点
LOCAL,12,0,9, R_FRONT -R_FRONT*cos(Theta),,-DEG_BACK 
*DO,I,D_BACK,L_BACK,D_BACK 
K,,I
*ENDDO 
K,,L_BACK
CSYS,0	! 后屋面上弦关键点
ALLSEL,ALL $CM,SX_KP,KP	!选择所有上弦关键点,建立组件"SX_KP"
*GET,SX_KMIN,KP,,NUM,MIN $*GET,SX_KMAX,KP,,NUM,MAX
*DO,I,SX_KMIN,SX_KMAX-1,1 $L,I,I+1 $*ENDDO	! 连接上弦杆
CM,SX_L,LINE	! 选择所有上弦梁,建立组件"SX_L"
CSYS,11 $K,,R_FRONT-D_D_HIGH,Theta-D_FRONT/5	
*DO,I,Theta-D_FRONT/2,D_FRONT/2,-D_FRONT $K,,R_FRONT-D_D_HIGH,I $*ENDDO
CSYS,0 
K,,9, R_FRONT -R_FRONT*cos(Theta)-D_D_HIGH	! 前屋面下弦关键点
LOCAL,13,0,9, R_FRONT -R_FRONT*cos(Theta)-D_D_HIGH,,-DEG_BACK
*DO,I,D_BACK,L_BACK-D_BACK,D_BACK $K,,I 
*ENDDO
K,,L_BACK-D_BACK 
CSYS,0	! 后屋面下弦关键点
KSEL,ALL $CMSEL,U,SX_KP $CM,XX_KP,KP	! 选择所有下弦节点,建立组件"XX_KP"
*GET,XX_KMIN,KP,,NUM,MIN $*GET,XX_KMAX,KP,,NUM,MAX $KSEL,ALL
*DO,I,XX_KMIN,XX_KMAX-1,1 $L,I,I+1 $*ENDDO	! 连接下弦杆
LSEL,ALL $CMSEL,U,SX_L $CM,XX_L,LINE $ALLSEL $GPLOT
! 选择所有下弦梁,建立组件"XX_L"
*DO,I,XX_KMIN+1,XX_KMAX-1,1 $L,I,I-SX_KMAX $L,I,I-SX_KMAX+1 $*ENDDO	! 前屋面腹杆
LSEL,ALL $CMSEL,U,SX_L $CMSEL,U,XX_L $CM,FG_L,LINE	! 选择所有腹杆，建立组件“FG_L”
KSEL,S,LOC,X,9 $*GET,SX_KMID,KP,,NUM,MIN $*GET,XX_KMID,KP,,NUM,MAX $ALLSEL
! =============================划分单元=============================
K,1000,4.5,1000 $CMSEL,,SX_L $LATT,1,,1,,1000,,1
CMSEL,,FG_L $LATT,1,,2,,,,3
CMSEL,,XX_L $LATT,1,,1,,1000,,2
ALLSEL $LESIZE,ALL,,,1 $LMESH,ALL $ALLSEL,ALL $NUMMRG,NODE
CMSEL,,SX_L $ESLL,S $NSLE,S,ACTIVE $*GET,SXN_NUM,NODE,0,COUNT
*GET,SXN_MIN,NODE,,NUM,MIN 
*DIM,SXZW,ARRAY,SXN_NUM,1 $*DIM,SXZW_LINE,ARRAY,SXN_NUM,2
SXZW(1,1)=NX(SXN_MIN) $SNX=SXN_MIN
*DO,I,2,SXN_NUM $SNX=NDNEXT(SNX) $SXZW(I,1)=NX(SNX) $*ENDDO
*VFUN,SXZW_LINE(1,2),ASORT,SXZW
*DO,I,1,SXN_NUM $CMSEL,,SX_L $ESLL,S $NSLE,S,ACTIVE $NSEL,R,LOC,X,SXZW_LINE(I,2)
*GET,SXZW_LINE(I,1),NODE,,NUM,MIN $*ENDDO	! 定义数组一，获取上弦节点
CMSEL,,XX_L $ESLL,S $NSLE,S,ACTIVE $*GET,XXN_NUM,NODE,0,COUNT
*GET,XXN_MIN,NODE,,NUM,MIN
*DIM,XXZW,ARRAY,XXN_NUM,1 $*DIM,XXZW_LINE,ARRAY,XXN_NUM,2
XXZW(1,1)=NX(XXN_MIN) $XNX=XXN_MIN
*DO,I,2,XXN_NUM $XNX=NDNEXT(XNX) $XXZW(I,1)=NX(XNX) $*ENDDO
*VFUN,XXZW_LINE(1,2),ASORT,XXZW
*DO,I,1,XXN_NUM $CMSEL,,XX_L $ESLL,S $NSLE,S,ACTIVE $NSEL,R,LOC,X,XXZW_LINE(I,2)
*GET,XXZW_LINE(I,1),NODE,,NUM,MIN $*ENDDO	! 定义数组二，获取下弦节点
ALLSEL,ALL
! =============================进入求解模块=============================
/SOLU $ANTYPE,STATIC 
DK,SX_KMIN,ALL $DK,SX_KMAX,ALL $DK,XX_KMIN,ALL
DK,XX_KMAX,ALL $SBCTRAN	! 施加约束
TIME,1 $NSUBST,1 $ACEL,,11.76	! 荷载步1,施加重力荷载
ALLSEL,ALL $LSWRITE,1 $LSCLEAR,INER	! 写为荷载步文件1
ALLSEL,ALL $TIME,2 $NSUBST,1
*DO,I,SX_KMIN+1,SX_KMID-1,1 $FK,I,FY,-HZZH2_FRONT_PRES*9/(SX_KMID-2) $*ENDDO
*DO,I,SX_KMID+1,SX_KMAX-1,1 $FK,I,FY,-HZZH2_BACK_PRES*1/(SX_KMAX-SX_KMID-1) $*ENDDO
CMSEL,S,SX_KP $KSEL,R,LOC,X,9 $FK,ALL,FY,-HZZH2_RIDGE_FK
*DO,I,XX_KMIN+1,XX_KMAX-1,1 $FK,I,FY,-HZZH2_LONG_PRES*10/(XX_KMAX-SX_KMAX-2) $*ENDDO
! 荷载步2
ALLSEL,ALL $SBCTRAN $LSWRITE,2 $FKDELE,ALL,ALL	! 写为荷载步文件2
ALLSEL,ALL $TIME,3 $NSUBST,1
*DO,I,SX_KMIN+1,SX_KMID-1,1 $FK,I,FY,-HZZH3_FRONT_PRES*9/(SX_KMID-2) $*ENDDO
CMSEL,S,SX_L $LSEL,R,LOC,X,0,9 $ESLL,R $SFBEAM,ALL,1,PRES,HZZH3_FRONT_WIND
*DO,I,SX_KMID+1,SX_KMAX-1,1 $FK,I,FY,-HZZH3_BACK_PRES*1/(SX_KMAX-SX_KMID-1) $*ENDDO
CMSEL,S,SX_L $LSEL,R,LOC,X,9,11 $ESLL,S $SFBEAM,ALL,1,PRES,HZZH3_BACK_WIND
CMSEL,S,SX_KP $KSEL,R,LOC,X,9 $FK,ALL,FY,-HZZH3_RIDGE_FK
*DO,I,XX_KMIN+1,XX_KMAX-1,1 $FK,I,FY,-HZZH3_LONG_PRES*10/(XX_KMAX-SX_KMAX-2) $*ENDDO
! 荷载步3
ALLSEL,ALL $SBCTRAN $LSWRITE,3 $FKDELE,ALL,ALL $SFEDELE,ALL,ALL,PRES
! 写为荷载步文件3
ALLSEL,ALL $TIME,4 $NSUBST,1
*DO,I,SX_KMIN+1,SX_KMID-1,1 $FK,I,FY,-HZZH4_FRONT_PRES*9/(SX_KMID-2) $*ENDDO
CMSEL,S,SX_L $LSEL,R,LOC,X,0,9 $ESLL,S $SFBEAM,ALL,1,PRES,HZZH4_FRONT_WIND
*DO,I,SX_KMID+1,SX_KMAX-1,1 
FK,I,FY,-HZZH3_BACK_PRES*1/(SX_KMAX-SX_KMID-1) $*ENDDO
CMSEL,S,SX_L $LSEL,R,LOC,X,9,11 $ESLL,S $SFBEAM,ALL,1,PRES,HZZH4_BACK_WIND
CMSEL,S,SX_KP $KSEL,R,LOC,X,9 $FK,ALL,FY,-HZZH4_RIDGE_FK
*DO,I,XX_KMIN+1,XX_KMAX-1,1 $FK,I,FY,-HZZH4_LONG_PRES*10/(XX_KMAX-SX_KMAX-2) $*ENDDO
! 荷载步4
ALLSEL $SBCTRAN $LSWRITE,4 $FKDELE,ALL,ALL $SFEDELE,ALL,ALL,PRES
! 写为荷载步文件4
OUTPR,ALL,ALL $ALLSEL,ALL $LSSOLVE,1,4,1	! 分步求解
! =============================进入后处理=============================
/POST1 $LCDEF,1,1 $LCDEF,2,2 $LCDEF,3,3 $LCDEF,4,4	! 定义荷载工况
LCASE,1 $LCOPER,ADD,3 $LCWRITE,13	! 荷载组合2
ALLSEL 
LCASE,1 
*GET,ZFL_G1,NODE,1,RF,FY
*GET,ZFL_G2,NODE, 128,RF,FY 
*GET,ZFL_G3,NODE, XXZW_LINE(1,1),RF,FY
*GET,ZFL_G4,NODE,255 ,RF,FY
YGL_M=(ZFL_G1+ZFL_G2+ZFL_G3+ZFL_G4)/11.76/10	! 求用钢量
LCASE,13 $*GET,RED_MAX12,SECR,,S,EQV,MAX	! 最大折算应力
ALLSEL
ETABLE,VOLU,VOLU
NSEL,ALL
SSUM
*GET,TVOL,SSUM,,ITEM,VOLU
TVOL= TVOL*10000
lgwrite,scratch,lgw
